plugins {
    id "java"
    id "idea"
    id "jaci.openrio.gradle.GradleRIO" version "2018.03.06"
    id 'com.diffplug.gradle.spotless' version '3.13.0'
    id 'net.ltgt.apt-idea' version '0.15'
}
def TEAM = 2767
def ROBOT_CLASS = "frc.team2767.Robot"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", jaci.openrio.gradle.frc.RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

repositories{
        jcenter()
        mavenLocal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // nanohttpd
}

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
// and NavX.
dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile 'org.strykeforce.thirdcoast:core:18.5.0'
    compile 'com.google.dagger:dagger:2.16'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.16'
    compileOnly 'org.jetbrains:annotations:15.0'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

spotless {
    java {
        googleJavaFormat()
        target '**/*.java'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}
